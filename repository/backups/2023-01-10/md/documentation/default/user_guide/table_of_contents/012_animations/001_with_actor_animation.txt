# ACTOR ANIMATION INSTRUCTION

**Actor Animation "name", Sequence$** : Is an instruction to easely define an animation (without a Spritesheet) and to play it with Actor.
- name is the name you give to that animation
- Sequence$ is a string with each image name or number separated by a comma, like this: "1,2,3,4"
 -- The last value can be "L" to loop, "E" to end, "R" to reverse and loop,  like this: "1,2,3,4,L"
 -- You can to set a delay between 2 images like this: "1:10, 2,..." to pause 1 sec. between image 1 and 2. Pauses are in tenth of a second :10=1 sec, like this "1:10,2,3,4,L"
 -- The images can be located in the resources/image folder or loaded from elsewhere with the Load Asset instruction.

 *Examples (will run if you have the images 1,2...6 in the image folder):*
-- Actor Animation "run", "1,2,3,4,5,6,L"
The animation named "run" will animates image 1.png, then 2,..., up to 6 then Loop back to 1
-- Actor Animation "walk", "1:10,2,3,4:5..."
The animation named "walk" will animates image 1.png, then wait 1sec, then images 2,3,4 then wait 0.5 sec.... 

*An other example:*
```basic
Actor Animation "gorun", Sequence$="1,2,3,4,L"
Actor "magic", Anim$="gorun"
Actor "Magic", AnimPlay=False //or True by default
```
The parameter AnimPlay=False will Stop (or True to Play Again) the animation.

Note: You need to have your Actor Animation instruction before using it with the corresponding Actor, otherwise it will not find the animation to run. See below example.
<BR>

*Example of a small game using animations:*
You will find all the images needed in the example below in the AOZ Studio Drive, located here: \aoz-editor\AOZ_Studio\app\Drives\AOZ Drive\resources\images\Platformer_MagicBird\...

```basic
// Backgrounds
Actor "forest", Image$="forest", StartX=0, EndX=-1920, Duration=20000, LoopMove=True
Actor "ground", Image$="ground",Y=880, StartX=0, EndX=-1920, Duration=5000, LoopMove=True


// All Actors Animations defined first
Actor Animation "run", "lucie_run_right-0, lucie_run_right-1, lucie_run_right-2, lucie_run_right-3, lucie_run_right-4, lucie_run_right-5, L"
Actor Animation "mechawalk", "magictoy_walk-0, magictoy_walk-1, magictoy_walk-2, magictoy_walk-3, magictoy_walk-4, magictoy_walk-5, L"
Actor Animation "roll","tagada_walk-0,tagada_walk-1,tagada_walk-2, L"
Actor Animation "star", "collectible-0, collectible-1, collectible-2, collectible-3, L"


// Play Lucie the hero
Actor "lucie", X=320, Y=636, Anim$="run", Behavior$="jump"

// Play the Mecha Monster 1
Actor "monster1", StartX=1920, EndX=-1920, Y=784, Duration=8000, Group$="badguy",Anim$="mechawalk",LoopMove=True
// Play the Mecha Monster 2
Actor "monster2", StartX=2880, EndX=-1920, Y=784, Duration=10000, Group$="badguy",Anim$="mechawalk",LoopMove=True
// Play the Rolling Monster
Actor "monster3", StartX=3360, EndX=-480, Y=740, Duration=9000, Group$="badguy", Anim$="roll",LoopMove=True
// Play the Star
Actor "star", StartX=4440, EndX=-2840, Y=500, Duration=9000, Anim$="star",LoopMove=True

Do
  If Actor Col( "lucie", Group$="badguy" ) = True Then Boom : End // was a collision between lucie and one of the backguy (the 3 monsters)
  Refresh
Loop
```

---
<p align="center"><img valign="middle" width="76px" src="https://doc.aoz.studio/assets/images/en/image001.png" />
</p>
<!--stackedit_data:
eyJoaXN0b3J5IjpbLTE0NjgxNjQ4MzcsMTUyODI0Mjk3NSwtOD
c2Mjc1NjE2LC0xMzA0MDU0OTI1LC01OTgzMTkxMSwxNTIxNzA3
NzkzLDg3MDIzMjAyNiw3MzA5OTgxMTZdfQ==
-->